plugins {
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "1.2.1"
    id "net.raphimc.class-token-replacer" version "1.0.0"
}

sourceSets {
    main {
        classTokenReplacer {
            property("\${version}", project.version)
        }
    }
}

dependencies {
    compileOnly gradleApi()

    api project(":runtime")
    api "net.lenni0451.classtransform:core:1.13.0"
    api "net.lenni0451.classtransform:additionalclassprovider:1.13.0"
}

java {
    withSourcesJar()
    withJavadocJar()
}

gradlePlugin {
    website = rootProject.website_url
    vcsUrl = rootProject.website_url
    plugins {
        methodinliner {
            id = "net.lenni0451.method-inliner"
            implementationClass = "net.lenni0451.methodinliner.MethodInlinerPlugin"
            displayName = project.maven_name ?: rootProject.maven_name
            description = rootProject.plugin_description
        }
    }
}

publishing {
    publications {
        pluginMaven(MavenPublication) {
            pom {
                name = rootProject.name
                description = rootProject.plugin_description
                url = rootProject.website_url
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/Lenni0451/GradleMethodInliner/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "Lenni0451"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/Lenni0451/GradleMethodInliner.git"
                    developerConnection = "scm:git:ssh://github.com/Lenni0451/GradleMethodInliner.git"
                    url = "github.com/Lenni0451/GradleMethodInliner"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.pluginMaven
}
